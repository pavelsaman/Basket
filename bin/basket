#!/usr/bin/env perl

use strict;
use warnings;

use Readonly;
use Pod::Usage;
use Getopt::Long;
use List::MoreUtils qw(uniq);
use lib './lib';
use Basket 0.002;

sub print_usage {
    my $usage_line = <<'END_USAGE';
Usage: basket [-l [-c category --before date --after date --dates]
               | -a -c category -i item
               | -d <-c category [ -i item ] | -i item>]
END_USAGE

    printf "%s", $usage_line;

    return;
}

sub print_help {
    my $help_message_start = <<'END_HELP_MSG_START';
Basket - keep track of your shopping lists
END_HELP_MSG_START
    my $help_message_end = <<'END_HELP_MSG_END';
Options:
    -l|--list       Lists all items, could be combined with:
                     -c|--category, --before, --after, --dates
    -a|--add        Adds a new item into a specified category
    -d|--delete     Deletes either the whole category (and therefore
                     all its items) or one item within a specified
                     category.

    -c|--category   Specifies a category name, could be used with --list,
                     has to be used with --add and --delete. Could re repeated
                     when used with --list and --delete without --item.
    -i|--item       Specifies an item, has to be used with --add and
                     with --delete when no --category is specified. Could be 
                     used with --delete when --category is specified.
    --before        Filters only items added before a certain date. Could 
                     be used with --list.
    --after         Filters only items added after a certain date. Could 
                     be used with --list.
    --dates         Prints when items were added along with their names. Could 
                     be used with --list.

    --usage         Prints the usage line.
    --help          Prints this message.
    --man           Prints the whole man page.
    --version       Prints the current version.    
END_HELP_MSG_END

    printf "%s\n", $help_message_start;
    print_usage();
    printf "\n%s", $help_message_end;

    return;
}

sub print_version {
    printf "%s: %.3f\n", qq{basket version:}, qq{$Basket::VERSION};

    return;
}

sub print_man {
    pod2usage(-exitval => 0, -verbose => 2);
}

sub pretty_print {
    my $data_ref    = shift;
    my $print_dates = shift;

    foreach my $cat (keys %$data_ref) {
        printf "%s:\n", $cat;
        foreach my $item (@{ $data_ref->{$cat}} ) {
            my @item_parts = split /;/, $item;
            printf " %s", $item_parts[0];
            if ($print_dates) {
                printf ";%s", $item_parts[1];
            }

            print "\n";
        }
    }

    return;
}

###############################################################################

# get command line options
my $result = GetOptions(
    # meta-options
    'usage'                => sub { print_usage();   exit 0; },
    'help'                 => sub { print_help();    exit 0; },
    'version'              => sub { print_version(); exit 0; },
    'man'                  => sub { print_man();     exit 0; },
    # behavioral options
    'l|list'               => \ my $list,
    'a|add'                => \ my $add,
    'd|delete'             => \ my $delete,
    'i|item=s{1,}'         => \ my @items,
    'c|cat|category=s{1,}' => \ my @categories,
    'after=s'              => \ my $after,
    'before=s'             => \ my $before,
    'dates'                => \ my $with_dates
);

# exactly one of --list, --delete, --add has to be given on the command line
my @given_verbs = grep { $_ } ($list, $add, $delete);

if (scalar @given_verbs != 1) {
    print_usage();
    exit 1;
}

my $basket = Basket->new({ dir => "./bin/files"});

if ($list) {
    if (scalar @items != 0) {
        print_usage();
        exit 1;
    }

    my $result;    
    if (scalar @categories > 0) {
        $result = $basket->list({
            categories => [uniq @categories],
            before     => $before,
            after      => $after
        });
    }
    else {
        $result = $basket->list({ before => $before, after => $after});
    }
    
    pretty_print($result, $with_dates);
}
elsif ($add) {
    # both item(s) and category(ies) have to be specified
    # maximum 1 category can be specified
    if (scalar @items == 0 or scalar @categories != 1) {
        print_usage();
        exit 1;
    }

    # add all items into basket
    foreach my $item (@items) {
        $basket->add_item({ category => $categories[0], text => $item });
    }
}
else {
    # either item(s) or category(ies) has to be specified
    if ((scalar @items > 1 and scalar @categories > 1)
        or (scalar @items == 0 and scalar @categories == 0)) {
        print_usage();
        exit 1;
    }
    
    # more items (from whatever categories) to delete
    if (scalar @items > 0 and scalar @categories == 0) {
        foreach my $item (@items) {
            $basket->delete_item({ text => $item });
        }
    }
    # more categories to delete
    elsif (scalar @categories > 0 and scalar @items == 0) {
        foreach my $category (@categories) {
            $basket->delete_category({ category => $category });
        }
    }
    # one category and one item given
    else {
        $basket->delete_item({ category => $categories[0], text => $items[0] });
    }
}

# save changes
$basket->save();

exit 0;